% Guess - generate all possible configurations 
% choice rule to assign exactly one room to each student
{room_assignment(S, R) : room(F, R, O, H)} = 1 :- student(S, G).

% Check - hard constraints 

% All rooms must have exactly their capacity in students

% Bounding potential number of students in a room to avoid getting stuck in grounding
num(0..229).

% Calculating how many students are in each room
num_in(R, N) :- #count{S : room_assignment(S, R)} = N, num(N), room(F, R, O, H).
% #show num_in/2.

% Limiting number of students per room to its capacity
:- room(F, R, 1, H), num_in(R, N), N!=1.
:- room(F, R, 2, H), num_in(R, N), N!=2.
:- room(F, R, 3, H), num_in(R, N), N!=3.

% % Doubles or triples must contain students of the same gender
:- room(F, R, O, H), room_assignment(S1, R), room_assignment(S2, R), student(S1, G1), 
    student(S2, G2), S1 != S2, G1 != G2.

% % Fill Ethridge from the bottom floor up (if there are any empty rooms, they should be on the top floor)
:- room(F1, R1, O1, H1), room(F2, R2, O2, H2), R1>R2, full_room(R2), empty_room(R1).

% Seeing which rooms are empty / full
full_room(R) :- room_assignment(S, R), student(S, G), room(F, R, O, H).
empty_room(R) :- not full_room(R), room(F, R, O, H).
% #show empty_room/1.

% % Prioritize accommodations for students with disabilities to be placed in single rooms
:- accomodation(S), room_assignment(S, R), room(F, R, O, H), O!=1. 

% Optimize 
% Trying to place people with their roommate choices
:~ room_assignment(S1, R), room_assignment(S2, R), roommate(S1, S2). [-1@4, S1, S2, R]

% helper predicate to see if roommates are assigned to each other 
% correct_roomie(S1, S2, R) :- room_assignment(S1, R), room_assignment(S2, R), roommate(S1, S2).
% #show correct_roomie/3.

% Trying to place people with their LLC
:~ room_assignment(S1, R1), room(F1, R1, O1, LLC), student_pref(S1, T1, LLC). [-1@3, S1, R1, F1, O1, LLC]

% helper predicate to make sure people are in correct LLC 
% in_LLC(S1, LLC) :- room_assignment(S1, R1), room(F1, R1, O1, LLC), student_pref(S1, T1, LLC).
% #show in_LLC/2.

% Trying to place people with their preferred room type
:~ student_pref(S, T, L), room_assignment(S, R), room(F, R, O, H), T = O. [-1@2, S, T, L, R, F, O, H]

% helper predicate to see if people are in correct room type
% correct_type(S, T, R) :- student_pref(S, T, L), room_assignment(S, R), room(F, R, O, H), T = O.
% #show correct_type/3.

% % Trying to place people who want two windows in corner rooms / rooms with two windows
:~ student_prefs2(S, F, 2), room_assignment(S, R), two_window(R). [-1@1, S, F, R]

% helper predicate to see if people are placed with two windows if they want them
% two_window_pref_good(S, R) :- student_prefs2(S, F, 2), room_assignment(S, R), two_window(R).
% #show two_window_pref_good/2.

#show room_assignment/2.